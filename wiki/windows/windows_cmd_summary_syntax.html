<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <link rel="Stylesheet" type="text/css" href="../styles/style.css" />
    <title>批处理常用命令总结 - 语句结构</title>
</head>
<body>
<div id="header">
    <ul id="top-nav">
    <li><a href="../index.html">首页</a></li>
    <li><a href="index.html">分类首页</a></li>
    </ul>
</div>
<div id="cse"></div>
<div id="main">

<h1>批处理常用命令总结 - 语句结构</h1>
<div class="toc">
<ul>
<li><a href="#toc_0.0.1">if语句(选择结构)</a>
<ul>
<li><a href="#toc_0.0.1.1">字符串比较</a>
<li><a href="#toc_0.0.1.2">存在判断</a>
<li><a href="#toc_0.0.1.3">定义判断</a>
</ul>
<li><a href="#toc_0.0.2">for语句(循环结构)</a>
<ul>
<li><a href="#toc_0.0.2.1">无开关</a>
<li><a href="#toc_0.0.2.2">开关/L</a>
<li><a href="#toc_0.0.2.3">开关/F</a>
<li><a href="#toc_0.0.2.4">开关/D或/R</a>
</ul>
<li><a href="#toc_0.0.3">字符串处理</a>
<ul>
<li><a href="#toc_0.0.3.1">截取字符串</a>
<li><a href="#toc_0.0.3.2">替换字符串</a>
<li><a href="#toc_0.0.3.3">字符串合并</a>
<li><a href="#toc_0.0.3.4">扩充字符串</a>
</ul>
<li><a href="#toc_0.0.4">数值计算</a>
</ul>
</ul>
</ul>
</div>

<p>
类似于C语言，批处理也有它的语句结构。批处理的语句结构主要有选择结构(if语句)、循环结构(for语句)等。
</p>

<h3 id="toc_0.0.1">if语句(选择结构)</h3>

<p>
if语句实现条件判断，包括字符串比较、存在判断、定义判断等。通过条件判断，if语句即可以实现选择功能。
</p>

<h4 id="toc_0.0.1.1">字符串比较</h4>

<p>
if语句仅能够对两个字符(串)是否相同、先后顺序进行判断等。其命令格式为：
<code>IF [not] string1 compare-op string2 command1 [else command2]</code>
</p>

<p>
其中，比较操作符compare-op有以下几类：
</p>
<ul>
<li>
== - 等于

<li>
EQU - 等于

<li>
NEQ - 不等于

<li>
LSS - 小于

<li>
LEQ - 小于或等于

<li>
GTR - 大于

<li>
GEQ - 大于或等于

</ul>

<p>
选择开关/i则不区分字符串大小写；选择not项，则对判断结果进行逻辑非。
</p>

<p>
字符串比较示例：
</p>
<pre>
===============================================
@echo off
set str1=abcd1233
set str2=ABCD1234
if %str1%==%str2% (echo 字符串相同！) else (echo 字符串不相同！)
if /i %str1% LSS %str2% (echo str1^&lt;str2) else (echo str1^&gt;=str2)
echo.
set /p choice=是否显示当前时间？(y/n)
if /i not %choice% EQU n echo 当前时间是：%date% %time%
pause&gt;nul
===============================================
</pre>

<p>
对于最后一个if判断，当我们输入n或N时的效果是一样的，都不会显示时间。如果我们取消开关/i，则输入N时，依旧会显示时间。
另外请注意一下几个细节：
</p>
<ul>
<li>
<code>echo str1^&lt;str2</code>

<li>
<code>echo str1^&gt;=str2</code>

<li>
<code>echo.</code>

</ul>

<h4 id="toc_0.0.1.2">存在判断</h4>

<p>
存在判断的功能是判断文件或文件夹是否存在。其命令格式为：
</p>
<pre>
IF [NOT] EXIST filename command1 [else command2]

===============================================
@echo off
if exist %0 echo 文件%0是存在的！
if not exist %~df0 (
echo 文件夹%~df0不存在！
) else echo 文件夹%~df0存在！
pause&gt;nul
===============================================
</pre>

<p>
这里注意几个地方：
</p>
<ol>
<li>
-存在判断既可以判断文件也可以判断文件夹；

<li>
-%0即代表该批处理的全称(包括驱动器盘符、路径、文件名和扩展类型)；

<li>
-%~df0是对%0的修正，只保留了其驱动器盘符和路径，详情请参考for /?，属高级批处理范畴；

<li>
-注意if语句的多行书写，多行书写要求command1的左括号必须和if在同一行、else必须和command1的右括号同行、command2的左括号必须与else同行、command1和command2都可以有任意多行，即command可以是命令集。

</ol>

<h4 id="toc_0.0.1.3">定义判断</h4>

<p>
定义判断的功能是判断变量是否存在，即是否已被定义。其命令格式为：
</p>
<pre>
IF [not] DEFINED variable command1 [else command2]
存在判断举例：
===============================================
@echo off
set var=111
if defined var (echo var=%var%) else echo var尚未定义！
set var=
if defined var (echo var=%var%) else echo var尚未定义！
pause&gt;nul
===============================================
</pre>

<p>
对比可知，"set var="可以取消变量，收回变量所占据的内存空间。
</p>

<h3 id="toc_0.0.2">for语句(循环结构)</h3>

<p>
for语句可以实现类似于C语言里面的循环结构，当然for语句的功能要更强大一点，通过不同的开关可以实现更多的功能。for语句有多个开关，不同开关将会实现不同的功能。
</p>

<h4 id="toc_0.0.2.1">无开关</h4>
<p>
无开关的for语句能够对设定的范围内进行循环，是最基本的for循环语句。其命令格式为：
</p>
<pre>
FOR %%variable IN (set) DO command
</pre>

<p>
其中，<code>%%variable</code>是批处理程序里面的书写格式，在DOS中书写为%variable，即只有一个百分号(%)；set就是需要我们设定的循环范围，类似于C语言里面的循环变量；do后面的command就是循环所执行的命令，即循环体。
</p>

<p>
无开关for语句举例：
</p>
<pre>
===============================================
@echo off
for %%i in (a,"b c",d) do echo %%i
pause&gt;nul
===============================================
</pre>

<h4 id="toc_0.0.2.2">开关/L</h4>

<p>
含开关/L的for语句，可以根据set里面的设置进行循环，从而实现对循环次数的直接控制。其命令格式为：
</p>
<pre>
FOR /L %%variable IN (start,step,end) DO command
</pre>

<p>
其中，start为开始计数的初始值，step为每次递增的值，end为结束值。当end小于start时，step需要设置为负数。
</p>

<p>
含开关/L的for语句举例(创建5个文件夹)：
</p>
<pre>
===============================================
@echo off
for /l %%i in (1,2,10) do md %%i
pause
===============================================
</pre>

<p>
上例将新建5个文件夹，文件夹名称依次为1、3、5、7、9。可以发现，<code>%%i</code>的结束值并非end的值10，而是不大于end的一个数。
</p>

<h4 id="toc_0.0.2.3">开关/F</h4>

<p>
含开关/F的for语句具有最强大的功能，它能够对字符串进行操作，也能够对命令的返回值进行操作，还可以访问硬盘上的ASCII码文件，比如txt文档等。其命令格式为：
</p>
<pre>
FOR /F ["options"] %%variable IN (set) DO command
</pre>

<p>
其中:
</p>
<ul>
<li>
set为("string"、'command'、file-set)中的一个；

<li>
options是(eol=c、skip=n、delims=xxx、tokens=x,y,m-n、usebackq)中的一个或多个的组合。

<li>
各选项的意义参见for /f。一般情况下，使用较多的是skip、tokens、delims三个选项。

</ul>

<p>
含开关/F的for语句举例：
</p>
<pre>
===============================================
@echo off
echo **No Options:
for /f %%a in ("1,2,10") do echo a=%%a
echo **Options tokens ^&amp; delims:
for /f "tokens=1-3 delims=," %%a in ("1,2,10") do echo a=%%a b=%%b c=%%c
pause
===============================================
@echo off
echo 本文件夹里面的文件有：
for /f "skip=5 tokens=3* delims= " %%a in ('dir') do (
if not "%%a"=="&lt;DIR&gt;" if not "%%b"=="字节" if not "%%b"=="可用字节" echo %%b
)
pause
===============================================
@echo off
echo 本文件夹里面的文件有：
dir&gt;c:/file.txt
for /f "skip=5 tokens=3* delims= " %%a in (c:/file.txt) do (
if not "%%a"=="&lt;DIR&gt;" if not "%%b"=="字节" if not "%%b"=="可用字节" echo %%b
)
del c:/file.txt
pause
===============================================
</pre>

<p>
对于后面的两个例子，其中options里面的delims= 是可以删除的，因为只要添加了/F开关系统就将delims的值默认为空格。
</p>

<p>
符号字符串中的最后一个字符星号，那么额外的变量将在最后一个符号解析之后分配并接受行的保留文本。本例中也可以改为4，不过文件名中有空格的文件，只能显示空格以前部分
</p>

<p>
同时我们也看到了，for语句的do后面的command也是可以分行的，只需要保证command的左括号和do在同一行就可以了。
</p>

<h4 id="toc_0.0.2.4">开关/D或/R</h4>

<p>
含开关/D或/R的for语句是与目录或文件有关的命令，一般情况下很少使用。含开关/R的命令有时候被用于通过遍历文件夹来查找某一个文件或文件夹，故而列举此例。
</p>

<p>
含开关/R的for语句举例(文件夹遍历)：
</p>
<pre>
===============================================
@echo off
setlocal enabledelayedexpansion
FOR /R d: %%i IN (.) DO (
set dd=%%i
set "dd=!dd:~0,-1!"
echo !dd!
)
pause
exit
===============================================
</pre>

<p>
上例即可以罗列出D盘下的所有文件夹，其速度要比命令"tree d:"慢多了，不过其返回结果的实用性则远远超过了tree命令。
</p>

<p>
一般情况下我们不推荐通过遍历文件夹来查找文件，特别是在查找某些程序(比如QQ.exe)的位置时。推荐通过reg命令查找注册表来查找QQ的路径，以保证查找效率。
</p>

<p>
上例中也出现了几个新面孔，如setlocal、感叹号等。其中，感叹号其实就是变量百分号(%)的强化版。之所以要用!而不用%，是因为在for循环中，当一个变量被多次赋值时，%dd%所获取的仅仅是dd第一次被赋予的值；要想刷新dd的值，就必须首先通过命令"setlocal enabledelayedexpansion"来开启延迟变量开关，然后用!dd!来获取dd的值。
</p>

<p>
for语句是批处理里面功能最强大、使用最普遍却又最难掌握的一套命令，这也是批处理菜鸟和批处理高手最明显的一个分水岭，一旦掌握了这套命令，那么你就离批处理达人不远了！
</p>

<h3 id="toc_0.0.3">字符串处理</h3>

<p>
批处理有着具有非常强大的字符串处理能力，其功能绝不低于C语言里面的字符串函数集。批处理中可实现的字符串处理功能有：截取字符串内容、替换字符串特定字段、合并字符串、扩充字符串等功能。下面对这些功能一一进行讲解。
</p>

<h4 id="toc_0.0.3.1">截取字符串</h4>

<p>
截取字符串可以说是字符串处理功能中最常用的一个子功能了，能够实现截取字符串中的特定位置的一个或多个字符。举例说明其基本功能：
</p>
<pre>
=========================================
@echo off
set ifo=abcdefghijklmnopqrstuvwxyz0123456789
echo 原字符串（第二行为各字符的序号）：
echo %ifo%
echo 123456789012345678901234567890123456
echo 截取前5个字符：
echo %ifo:~0,5%
echo 截取最后5个字符：
echo %ifo:~-5%
echo 截取第一个到倒数第6个字符：
echo %ifo:~0,-5%
echo 从第4个字符开始，截取5个字符：
echo %ifo:~3,5%
echo 从倒数第14个字符开始，截取5个字符：
echo %ifo:~-14,5%
pause
=========================================
</pre>

<p>
当然，上面的例子只是将字符串处理的基本功能展示出来了，还看不出字符串处理具体有什么用处。下面这个例子是对时间进行处理。
</p>
<pre>
=========================================
@echo off
echo 当前时间是：%time% 即 %time:~0,2%点%time:~3,2%分%time:~6,2%秒%time:~9,2%厘秒
pause
=========================================
</pre>

<h4 id="toc_0.0.3.2">替换字符串</h4>

<p>
替换字符串，即将某一字符串中的特定字符或字符串替换为给定的字符串。举例说明其功能：
</p>
<pre>
=========================================
@echo off
set aa=伟大的中国！我为你自豪！
echo 替换前：%aa%
echo 替换后：%aa:中国=中华人民共和国%
echo aa = %aa%
set "aa=%aa:中国=中华人民共和国%"
echo aa = %aa%
pause
=========================================
</pre>

<p>
对于上面的例子有一点说明，对比两个echo aa = %aa%可以发现，如果要修改变量aa的内容的话，就需要将修改结果“%aa:中国=中华人民共和国%”赋值给变量aa。上面的字符串截取也有着同样的特点。
</p>

<h4 id="toc_0.0.3.3">字符串合并</h4>

<p>
其实，合并字符串就是将两个字符串放在一起就可以了。举例说明：
</p>
<pre>
=========================================
@echo off
set aa=伟大的中国！
set bb=我为你自豪！
echo %aa%%bb%
echo aa=%aa%
echo bb=%bb%
set "aa=%aa%%bb%"
echo aa=%aa%
pause
=========================================
</pre>

<p>
同样，如果要改变变量aa的内容的话，就需要将合并结果“%aa%%bb%”赋值给变量aa。
</p>

<h4 id="toc_0.0.3.4">扩充字符串</h4>

<p>
“扩充”这个词汇来自于微软自己的翻译，意思就是对表示文件路径的字符串进行特殊的处理，具体功能罗列如下：
</p>

<pre>
=========================================
~I - 删除任何引号(")，扩充 %I
%~fI - 将 %I 扩充到一个完全合格的路径名
%~dI - 仅将 %I 扩充到一个驱动器号
%~pI - 仅将 %I 扩充到一个路径
%~nI - 仅将 %I 扩充到一个文件名
%~xI - 仅将 %I 扩充到一个文件扩展名
%~sI - 扩充的路径只含有短名
%~aI - 将 %I 扩充到文件的文件属性
%~tI - 将 %I 扩充到文件的日期/时间
%~zI - 将 %I 扩充到文件的大小
%~$PATH:I - 查找列在路径环境变量的目录，并将 %I 扩充
到找到的第一个完全合格的名称。如果环境变量名
未被定义，或者没有找到文件，此组合键会扩充到
空字符串
可以组合修饰符来得到多重结果:
%~dpI - 仅将 %I 扩充到一个驱动器号和路径
%~nxI - 仅将 %I 扩充到一个文件名和扩展名
%~fsI - 仅将 %I 扩充到一个带有短名的完整路径名
%~dp$PATH:i - 查找列在路径环境变量的目录，并将 %I 扩充
到找到的第一个驱动器号和路径。
%~ftzaI - 将 %I 扩充到类似输出线路的 DIR
=========================================
</pre>

<p>
以上内容引用于for /?帮助信息。其中的I代表变量I，不过需要说明的是，不是所有的变量都能够进行扩充的，有两个条件：1、该字符串代表一个文件路径；2、变量要用%x来表示，x可取a-z A-Z 0-9共62个字符中的任意一个。举例说明：
</p>
<pre>
=========================================
@echo off
echo 正在运行的这个批处理：
echo 完全路径：%0
echo 去掉引号：%~0
echo 所在分区：%~d0
echo 所处路径：%~p0
echo 文件名：%~n0
echo 扩展名：%~x0
echo 文件属性：%~a0
echo 修改时间：%~t0
echo 文件大小：%~z0
pause
=========================================
</pre>

<p>
其中的%0是批处理里面的参数，代表当前运行的批处理的完全路径。类似的还有%1-%9，分别代表传递来的第1-9个参数。例子如下：
</p>
<pre>
===============================================
@echo off
set aa=C:/Windows/PPP/a.btx
call :deal aaa %aa% "c c" ddd eee
pause&gt;nul
exit
:deal
echo %%0 = %0
echo %%1 = %1
echo %%2 = %2
echo %%3 = %3
echo %%4 = %4
echo %%5 = %5
===============================================
</pre>

<p>
其中，变量aa在之前是不可以扩充的，通过call命令并将aa作为参数传递给子函数:deal，将aa变量转换成了变量%1，即符合%x格式，从而可以进行字符串扩充。
</p>

<p>
至于%x中x取a-z A-Z的形式，可以复习一下for语句，for语句里面的变量就是用%x来表示的，因而可以直接进行扩充。
</p>

<h3 id="toc_0.0.4">数值计算</h3>

<p>
批处理里面的数值计算功能较弱，只能够进行整型计算，忽略浮点数的小数部分；同时数值计算的范围也受限于系统位数，对于目前较为常见的32位机来说，数值计算能处理的数值范围为0x80000000h~0x7FFFFFFFh，即-2147483648~+2147483647。
</p>

<p>
数值计算需要使用set命令，具体格式为<code>set /a expression</code>。其中，expression代表计算表达式，计算表达式跟C语言里面的表达式基本上完全一致。set支持的运算符也跟C语言里面的一样，只是没有了増一减一。set支持的运算符及优先级排序如下：
</p>
<pre>
=========================================
() - 分组
! ~ - - 一元运算符（逻辑非、按位非、取负）
* / % - 算数运算符（乘、除得商、除得余数，即取余）
+ - - 算数运算符（加、减）
&lt;&lt; &gt;&gt; - 逻辑移位（左移一位、右移一位）
&amp; - 按位“与”
^ - 按位“异”
| - 按位“或”
= *= /= %= += -= - 赋值
&amp;= ^= |= &lt;&lt;= &gt;&gt;=
, - 表达式分隔符（set可一次处理多个表达式）
=========================================
</pre>

<p>
我们知道，批处理中取变量的值是需要用%或者!的，而在set /a 中，直接用变量名称即可取得变量的值。另外，set支持八进制（数字前缀0）、十进制（数字无前缀）和十六进制（数字前缀0x），且支持不同进制之间的计算，如set /a a=123+0123+0x123，计算及显示结果为十进制。
</p>

</div>
<div id="footer">
<p>
&copy; 2012 - 2015 XStar
&nbsp;|&nbsp;<a href="http://code.google.com/p/vimwiki/" title="vimwiki">Powerby:Vimwiki</a>
&nbsp;|&nbsp;<a href="http://kwiki.github.io" title="丘迟">Style:丘迟</a>
&nbsp;|&nbsp;<a href="../index.html">首页</a>
&nbsp;|&nbsp;<a href="index.html">分类首页</a>
&nbsp;|&nbsp;<a href="../SiteMap.html">站点地图</a>
</p>
</div>
<script type="text/javascript">var vimwiki_rootpath="../";</script>
<script type="text/javascript" src="http://cdn.staticfile.org/jquery/2.0.0/jquery.min.js"></script>
<script type="text/javascript" src="../scripts/vimwiki.js"></script>
</body>
</html>

